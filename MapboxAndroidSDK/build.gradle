buildscript {

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.+'
        classpath 'com.github.dcendents:android-maven-plugin:1.0'
    }

}

apply plugin: 'android-library'
apply plugin: 'android-maven'

group = 'com.mapbox.mapboxsdk'
version = '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
    flatDir {
        dirs '../libs'
    }
}

dependencies {
    compile ('com.github.chrisbanes.bitmapcache:BitmapCache:2.4-SNAPSHOT@aar') {
        transitive:true
        exclude module: 'support-v4'
    }
    compile fileTree(dir: 'libs', include: '*.jar')
//    compile (project(':Android-BitmapCache')) {
//        transitive:true
//        exclude module: 'support-v4'
//    }
    compile 'com.android.support:support-v4:19.0.+'
    compile 'com.squareup.okhttp:okhttp:1.3.0'
    compile 'com.nineoldandroids:library:2.4.0'

}

android {

    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    sourceSets {
        main {

            manifest.srcFile 'src/main/AndroidManifest.xml'

            java.srcDirs = ['src/main/java']

            resources.srcDirs = ['src/main/resources']

            res.srcDirs = ['src/main/res']

            assets {
                srcDir 'assets'
            }
        }
    }
    lintOptions {
        abortOnError false
    }
}

android.libraryVariants.all { variant ->

    task("generate${variant.name}Javadoc", type: Javadoc) {
        title = "$name $version API"
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar = "${android.plugin.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
        options.links("http://docs.oracle.com/javase/7/docs/api/");
        options.links("http://d.android.com/reference/");
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
    }

}